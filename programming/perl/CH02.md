## CH02 Meta Operators

### What are the meta operators

There are 6 types of Meta-operators that each enhance the behavior of the regular operators. Some of these operators can also be found in Perl 5 and in other languages. Others are more special to Perl 6. 

- assignment operators
- negated relation operators
- reversed operators
- hyper operators
- reduction operators
- cross operators

See also [S03](http://perlcabal.org/syn/S03.html#Meta_operators)。

> Note：  
> In fact, not only these 6 operators:
> - substitution operators
> - zip operators
> - sequential operators
> 
> More details [Meta Operators](https://docs.perl6.org/language/operators#___top)。

### Assignment Operators  

**tutorial/arrays/assignment_shortcut.p6**  
```perl
#!/usr/bin/env perl6
use v6;

my $num = 23;
say $num + 19;     # 42
say $num;          # 23

$num += 19;
say $num;          # 42
```

### Method invocation in assignment

In Perl 6 it extends to the `.` operator that allows the call of methods on objects.

If you'd like to change the original string you can write: `$str = $str.subst('B', 'X');`  

**tutorial/arrays/assignment_operators.p6**
```perl
#!/usr/bin/env perl6
use v6;

my $str = 'ABBA';
say $str.subst('B', 'X');    # AXBA
say $str;                    # ABBA

say $str .= subst('B', 'X'); # AXBA
say $str;                    # AXBA
```

### Calling a function during assignment

The same can be used with functions so instead of writing `my $lower = min($lower, $new_value)`; you can write `$lower min= $new_value`. 

**tutorial/arrays/assignment_function_shortcuts.p6**
```perl
#!/usr/bin/env perl6
use v6;

my $lower = 2;
$lower min= 3;
say $lower;         # 2

$lower min= 1;
say $lower;         # 1
```

This should even work with the comma operator effectively pusing more values on an array but it is not yet implemented in Rakudo.

```perl
my @a = (1, 2, 3);
@a ,= 4;
@a.say;
```

### Negated Relation Operators

- `==` for comparing numerical values
- `eq` for comparing strings
- The negated version would look like `!==` and `!eq`
- Short-cut version that are spelled `!=` and `ne` as one
- `!>=` which means less than (for numbers) and `!ge` which is the same for strings  


> ```perl
> 2 !>= 3; # True
> 2 !>= 2; # False
> 2 !>= 1; # False
> 
> "aaa" !ge "aab"; # True
> "aaa" !ge "aaa"; # False
> "aaa" !ge "aa"; # False
> ```

if you create an operator called `I` then you will automatically get one that looks like `!I` which would be its negation.

**tutorial/arrays/negated_operators.p6**
```perl
#!/usr/bin/env perl6
use v6;

#Equality
say 1 ==  1 ?? 'y' !! 'n'; # y
say 1 !== 1 ?? 'y' !! 'n'; # n
say 1 !=  1 ?? 'y' !! 'n'; # n

say 'ac' eq  'dc' ?? 'y' !! 'n'; #n
say 'ac' !eq 'dc' ?? 'y' !! 'n'; #y

say 1 <  2  ?? 'y' !! 'n'; # y
####say 1 !< 2  ?? 'y' !! 'n'; # n

say 1 <=  2  ?? 'y' !! 'n'; # y
####say 1 !<= 2  ?? 'y' !! 'n'; # n

say 1 >=  2  ?? 'y' !! 'n'; # n
####say 1 !>= 2  ?? 'y' !! 'n'; # y
```

### Reversed operators

Reversed operators will swap the position of the two operands. 

So instead of switching the two arguments as in `$b cmp $a` one can write `$a Rcmp $b`. 
  
> ```perl
> 2 / 4; # 0.5
> 2 R/ 4; # 2
>
>#spaceship operator, output 0, 1, -1
>
> 1 <=> 2; # Less
> 2 <=> 1; # More
> 1 R<=> 2; # More
> 2 R<=> 1; # Less
>
> 1 <=> 1; # Same
> my $result = 1 <=> 1; # Same
> 1 + $result; # 1
> $result = 1 <=> 2; # Less
> 1 + $result; # 0
>
> "a" gt "b"; # False
> "a" Rgt "b"; # True


### Hyper Operators

They allow the extrapolation of a scalar operator to operate on a list of scalars. 

The real operators are actually unicode characters(» and «) but using **regular doubled angle-brackets** also work.

Normally the arrows **point inside**, towards the operator and there are two lists on the two sides of the operator. 

If the list on one side is **longer than** on the other side perl throws an exception "Lists on either side of non-dwimmy hyperop of infix are not of the same length".

> Note：  
> - Unicode characters `»`(\x[BB]) and `«`(\x[AB]).
> ```perl
> (1, 2, 3) »+» 1; # (2 3 4)
> (1, 2, 3) >>+>> 1; # (2 3 4)
> ```
> - DWIM(do what i means)，DWIMMY is adj type.

When the arrows point to shorter side, using that side scalar DWIM. Not sure which one is shorter, you can DWIM both sides: `$left «*» $right;`  

If you want the operators become DWIMMY, let the arrows point to the DWIM side or point to both sides.

You can make both sides dwimmy if you don't know up front which one will be longer and if you want to make them work in both ways. 


**tutorial/arrays/hyper.p6**
```perl
#!/usr/bin/env perl6
use v6;

my @x = (1, 2) >>+<< (3, 4);
say @x.perl;  # [4, 6]

#my @d = (1, 2) >>+<< (3);
#say @d.perl;  # [4, 6]
#Non-dwimmy hyperoperator cannot be used on arrays of different sizes or dimensions

my @z = (1, 2, 3, 4) >>+>> (1, 2);
say @z.perl;      # [2, 4, 4, 6]

@z = (1, 2, 3, 4) <<+>> (1, 2);
say @z.perl;      # [2, 4, 4, 6]

@z = (4) <<+>> (1, 2);
say @z.perl;      # [5, 6]

my @y = (1, 2) >>+>> 1;
say @y.perl;      # [2, 3]
```

There is a lot more to hyper operators but for now this should be enough. 

**tutorial/arrays/hyper.p6.out**
```perl
Array.new(4, 6)
Array.new(2, 4, 4, 6)
Array.new(2, 4, 4, 6)
Array.new(5, 6)
Array.new(2, 3)
```

### Reduction operators

**tutorial/arrays/reduction_operators.p6**
```perl
#!/usr/bin/env perl6
use v6;

say [+] 1, 2;   # 3
say [+] 1..10;  # 55

#factorial
say [*] 1..5;   # 120

say [**] 2,2,2; # 16    == 2**2**2

my @numbers = (2, 4, 3);

#check if the numbers are in growing order
say [<] @numbers;      # False   

say [<] sort @numbers; # True
```

### Reduction Triangle operators

The `~` in front of the operator is only needed for the stringification of the list to inject spaces between the values when printed. 

> The operators don't reduce all items of list to one item, it still return list

**tutorial/arrays/triangle_operators.p6**
```perl
#!/usr/bin/env perl6
use v6;

say ~[\+] 1..5;  # 1 3 6 10 15
say ~[\*] 1..5;  # 1 2 6 24 120
```

### Cross operators

**tutorial/arrays/cross_operators.p6**
```perl
#!/usr/bin/env perl6
use v6;

my @x = (1, 2) X+ (4, 7);
say @x.perl;        # [5, 8, 6, 9]

my @y = 1, 2 X+ 4, 7;
say @y.perl;        # [5, 8, 6, 9]

my @str = 1, 2 X~ 4, 7;
say @str.perl;      # ["14", "17", "24", "27"]


#without any special operator  (is the same what X, should be)
my @z = 1, 2 X 4, 7;
say @z.perl;        # [(1, 4), (1, 7), (2, 4), (2, 7)]
```
