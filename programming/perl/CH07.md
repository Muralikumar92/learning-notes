## Perl 5 to Perl 6

### Intro

- While there are many similarities between Perl 5 and Perl 6 there are also a few substantial changes. 
- What I am trying to show here is that given a Perl 5 program how one can more-or-less straight forward translate it to Perl 6. 


### Hello World

**tutorial/p526/hello_world.p6**

```perl
use v6;

#Perl 5: print
#Perl 5.10: say

#Perl 6: print still works and there is say()
#just like in 5.10 adding a newline automatically

print "Hello World\n";
say "Hello World"; 
```

### Scalars

**tutorial/p526/scalars.p6**

```perl
use v6;

#Perl 5: my
#Perl 6: In Perl 6 you always need to have "my" (think as always use strict)
my $name = "Moose";

#Perl 6: Scalar variables still interpolate in double quoted strings:
say "Hello $name";
#In order to make the Object Oriented people happy Perl 6 also allows this:
"Hello $name".say;


#Perl 5: <STDIN>
#Perl 6: As reading a single line from standard input
        #is usually preceeded by a print statement the keyword to do that in Perl 6
        #is called prompt()
        #it chomps the newline off automatically.
my $line = prompt('Please type in your name: ');
say "Hi $line, how are you?";

#Perl 5: substr
#substr works as before but there is currently no 4 parameter version.
say substr $line, 2, 3;

#it also works this way
$line.substr(2, 3).say;


#Perl 5: length
my $a = "This is a string";
#Perl 6: Does not have a length() function, instead it has several
#functions with more precise meaning of the type of unit you are asking for:
#bytes, chars
say $a.chars;
say $a.bytes;

#There are also graphs and codes but they are not yet implemented in Rakudo
#see S32-setting-library/Str.pod for details



#Perl 5: chomp
#Perl 6: It will be used a lot less in Perl 6 as reading lines in Perl 6
#automatically chomps off the newline. In any case, in Perl 6 chomp returns
#the chomped string and does NOT change the original
my $b = chomp $a;

#Perl 5: defined
#Perl 6: is the same:
defined $b;

#Perl 5: undef
#Perl 6: is NOT the same !
#$b = undef;


#Perl 5: .
#Perl 6: String concatenation is now done with the ~ operator
my $str = "Foo" ~ "Bar";
$str.say;

$str ~= " and Moo";
say $str;

#Perl 5: x
#Perl 6: string repetition stayed the same
say "abc" x 3;

#Perl 5: index and rindex
#Perl 6: are the same
my $s = "The brown cat climbed the green tree";
index $s, "b";

#Perl 5: lc, uc
#Perl 6: are the same
say lc "Hello World";
say uc "Hello World";
```

### Arrays

**tutorial/p526/arrays.p6**

```perl
use v6;

#Creating an array in Perl 6 can be the same as in Perl 5
my @numbers = ("one", "two", "three");

#For debugging prints one will use the .perl method instead of Data::Dumper:
@numbers.perl.say;   # ["one", "two", "three"]


#In Perl 6 there is no need for the parentheses:
my @digits = 1, 3, 6;
@digits.perl.say;  # [1, 3, 6]


#The Perl 5 qw() operator is replaced by the angle brackets:
my @names = <foo bar baz>;
@names.perl.say;  # ["foo", "bar", "baz"]


#Arrays do NOT interpolate any more in double quoted strings:
say "@names";                        # @names

#So you can safely write the following without escaping the at mark:
say "joe@names.org";                 # joe@names.org

#If you do want to interpolate the values of an array you have to put it inside curly braces:
say "names: {@names}";               # names: foo bar baz


#When accessing elements of an array in Perl 6 the sigil does NOT change!
#This will be very strange for Perl 5 programmers but it has some advantages
#on the long run.

say @names[0];    # foo


#Instead of the quite ugly $#array notation used in Perl 5
#fetching the number of elements of array is done in Perl 6 by the
#elems() function
#Though I think the object oriented writing is much nicer here:
say @names.elems;                   # 3


#Ranges are available in Perl 6 just as in Perl 5
my @d = 1..11;
@d.perl.say;    # [1, 2, 3, 4, 5, 6, 7, 8, 9, 10, 11]

#It works with characters as well
my @chars = ('a' .. 'd');
@chars.perl.say;    # ["a", "b", "c", "d"]

#The C-style version of the Perl 5 "for" loop is now called "loop"
#but I won't show it here as it is way better to use the
#the iterator style for loop that most people in Perl 5 write as "foreach".
#In Perl 6 it is spelled as "for".

for 1..3 -> $i {
    say $i;       # 1   # 2    # 3     (on 3 lines)
}

#The same works on arrays as well:

for @names -> $n {
    say $n;        # foo  # bar # baz    (on 3 lines)
}

#This is one of the cases where you don't need to declare a variable
#using "my". The looping variable is automatically declared for you
#and scoped to the block of the for loop.


#If you need to iterate over the indexes of an array then you could write
for 0..@names.elems -1 -> $i {
    say "$i {@names[$i]}"; # 0 foo       # 1 bar       # 2 baz    (on 3 lines)
}

#Or you can pick one of the suggestions I got from Moritz Lenz on #perl6 (Thanks Moritz!):
for @names.keys -> $i {
    say "$i {@names[$i]}"; # 0 foo       # 1 bar       # 2 baz    (on 3 lines)
}

#The keys method borrowed from the hashes will return all the indexes of an array.
#Even if your array has "holes" in it, elements with undef as value, key will
#still include the indexes of such elements.


#split acts similar to its Perl 5 version but the defaults do not apply any more
#and you should read the spec anyway.
#See S32-setting-library/Str.pod for more details.
#( http://perlcabal.org/syn/S32/Str.html )

"a,b,c".split(',').perl.say;    # ["a", "b", "c"]
```

### Hashes

**tutorial/p526/hashes.p6**

```perl
#!/usr/bin/env perl6
use v6;

my %h = ("Perl", "Larry", "Python", "Guido");      # creates hash as in Perl5
#Perl5: hash element
say %h{"Perl"};                                    # accessing hash elements is simialr but the sigil remains %
#Perl5: hash key without quotes
say %h<Perl>;                                      # if you want to leave out the quotation marks "" (was $h{Perl} in Perl5)

#Perl5: exists
%h.exists("Perl");                                 # checks if key exists (formally the exists function)
defined(%h<Perl>);                                 # checks if the value of the give key is defined

#Perl5: keys
keys %h;                                           # returns a list of keys just as in Perl5
%h.keys;                                           # also works

#Perl5: values

#Perl5: each
#%h.kv
for %h.kv -> $key, $value {
    say "$key => $value";
}
```

### Control Structures

**tutorial/p526/control_structures.p6**

```perl
use v6;

#Perl 5: if
#Perl 6: Same, but no need to for () around the condition
my $x = 23;
if $x < 42 {
}


#Perl5: foreach (for)
my @names = <Larry Guido Matz>;
for @names -> $person {
    say $person;
}

#Perl5: for (foreach)
loop (my $i=1; $i < 10; $i++) {
    say $i;
}

#infinite loop:
loop {
    #well, almost infinite :-)
    last;
}


#Perl5: while
while $i < 10 {
    say $i;
}
```

### Functions

**tutorial/p526/functions.p6**

```perl
#!/usr/bin/env perl6
use v6;

my $lang = "Perl";

creator("Larry", $lang);
sub creator {                               # still works as in Perl5 and you get the values in @_
    my ($person, $language) = @_;
    say "the creator of $language is $person";
}

creator_2("Larry", $lang);
sub creator_2 ($person, $language) {            # you can also defined the expected parameters
    #$language ~= 6;                            # but in this case you cannot change the variables
    say "the creator of $language is $person";
}

creator_3("Larry", $lang);
sub creator_3 ($person, $language is rw) {            # you can also defined the expected parameters
    $language ~= 6;                                   # now you can change the $language variable but
    say "the creator of $language is $person";
}
say "What ? $lang";                                   # and it also changes the parameter itself

creator_4("Larry", $lang);
sub creator_4 ($person is copy, $language is rw) {
    $person ~= " Wall";
    say "the creator of $language is $person";
}
```

### Files

**tutorial/p526/files.p6**

```perl
#!/usr/bin/env perl6
use v6;

#Perl5: open
my $fh = open 'examples/p526/files.p6', :r;

#Perl5: close
close($fh);
$fh.close

#Perl5: <$fh>
#$fh.readline
```

### Modules, Classes

**tutorial/p526/modules.p6**

```perl
#!/usr/bin/env perl6
use v6;

#Perl5: package
module Person {
    our $fname;
    our $lname;
}
$Person::fname = "Foo";
$Person::lname = "Bar";
```

### Perl 5 to Perl 6


```perl
@ARGV                    @$ARGS
$0                       $*PROGRAM-NAME
foreach my $s (@arr) {}  for @arr -> $s { }

$#array
scalar @array            @array.elems
```
