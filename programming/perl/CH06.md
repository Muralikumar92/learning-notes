## Perl 6 Hashes

### Hashes (Associative Arrays)

A hash (also called associative array) is a set of key,value pairs where the keys are **unique strings** and the values can have any, err value.

Hashes always start with a `%` (percentage) sign. 

**tutorial/hash/create_hash.p6**

```perl
#!/usr/bin/env perl6
use v6;

my %user_a = "fname", "Foo", "lname", "Bar";

my %user_b = 
    "fname" => "Foo", 
    "lname" => "Bar",
;

say %user_a{"fname"};
%user_a{"email"} = "foo@bar.com";
say %user_a{"email"};

say %user_b<lname>;
```

output:

**tutorial/hash/create_hash.p6.out**

```
Foo
foo@bar.com
Bar
```

### Fetching data from a hash

**tutorial/hash/print_hash.p6**

```perl
#!/usr/bin/env perl6
use v6;

my %user = 
    "fname" => "Foo", 
    "lname" => "Bar",
    "email" => "foo@bar.com",
;

for %user.keys.sort -> $key {
    say "$key  %user{$key}";
}
```

output:

**tutorial/hash/print_hash.p6.out**

```
email  foo@bar.com
fname  Foo
lname  Bar
```

### Multidimensional hashes

**tutorial/hash/multi.p6**

```perl
#!/usr/bin/env perl6
use v6;

my %xml;

%xml<person>[0] = 'Foo';
%xml<person>[1] = 'Bar';

say %xml.perl;
```

output:

**tutorial/hash/multi.p6.out**

```
{:person($["Foo", "Bar"])}
```

### Count words

**tutorial/hash/count_words.p6**

```perl
#!/usr/bin/env perl6
use v6;

my $filename = 'examples/words.txt';

my %counter;

my $fh = open $filename;
for $fh.lines -> $line {
    my @words = split /\s+/, $line;
    for @words -> $word {
        %counter{$word}++;
    }
}

for %counter.keys.sort -> $word {
    say "$word {%counter{$word}}";
}
```

### Overview of hashes

**tutorial/hash/hash.p6**

```perl
#!/usr/bin/env perl6
use v6;

#creating hashes
my %h1 = first => '1st', second => '2nd';

if %h1{'first'}.defined {
    say "the value of 'first' is defined";
}
if %h1<second>.defined {
    say "the value of 'second' is defined";
}

say %h1{'third'}.defined ?? "third exists" !! "third does not exist";

say %h1<first>;
say %h1<second>;

#TODO hash with fixed keys not implemented yet
#my %h2{'a', 'b'} = ('A', 'B');
#say %h2.delete('a');
#say %h2.delete('a');
```

输出：

**tutorial/hash/hash.p6.out**

```
the value of 'first' is defined
the value of 'second' is defined
the key 'first' exists in h2
third does not exist
1st
2nd
```

### slurp hash

**tutorial/files/slurp_csv_file.p6**

```perl
#!/usr/bin/env perl6
use v6;

my $filename = 'examples/phonebook.txt';

my @lines = lines $filename.IO;
for @lines -> $line {
    say "L: $line";
}

#my %phonebook = map {split ",", $_}, @lines;
#my %phonebook = map {$_.comb(/\w+/)}, @lines;

my %phonebook = slurp($filename).comb(/\w+/);

my $name = prompt "Name:";
say %phonebook{$name};
```

**tutorial/phonebook.txt**

```
Foo,123
Bar,78
Baz,12321
```

### kv

**tutorial/hash/print_hash_kv.p6**

```perl
#!/usr/bin/env perl6
use v6;

my %user =
    "fname" => "Foo",
    "lname" => "Bar",
    "email" => "foo@bar.com",
;

for %user.kv -> $key, $value {
    say "$key  $value";
}
```

output:

**tutorial/hash/print_hash_kv.p6.out**

```
fname  Foo
lname  Bar
email  foo@bar.com
```

### Looping over keys of a hash

- The same way we can loop over keys of a hash - after fetching them using the `keys` function.
- The declaration of hashes in Perl 6 is similar to that in Perl 5 but when access individual elements in the hash it now keeps the `%` prefix. 
- Thus the value of the key `"Foo"` will be `%phone{"Foo"}`.
- Similarly if `$name` contains `"Foo"` we can use the `%phone{$name}` expression to get back the relevant value.

**tutorial/hash/loop_over_hash.p6**

```perl
#!/usr/bin/env perl6
use v6;

my %phone = 
    "Foo" => 123,
    "Bar" => 456,
;

for keys %phone -> $name {
    say "$name %phone{$name}";
}
```

output:

**tutorial/hash/loop_over_hash.p6.out**

```
Bar 456
Foo 123
```
