## Perl 6 Lists and Arrays

### List Literals, list ranges

- Things in () separated by commas are called a list of things. 
- Actually you don't even need the parentheses for the list, it just clarifies precedence. 

> Note：  
> ```perl
> ("a", "b", 0);
> #same with:
> "a", "b", 0;
> ```

- A list is an ordered set of scalar values.

**tutorial/arrays/list_literals.p6**

```perl
#!/usr/bin/env perl6
use v6;

(1, 5.2, "apple");          # 3 values

(1,2,3,4,5,6,7,8,9,10);     # nice but we are too lazy, so we write this:
(1..10);                    # same as (1,2,3,4,5,6,7,8,9,10)
(1..Inf);                   # represents the list of all the numbers
(1..*);                     # this too

("apple", "banana", "peach", "blueberry");   # is the same as
<apple banana peach blueberry>;              # quote word

my ($x, $y, $z);               # We can also use scalar variables as elements of a list
```

> Note：  
> ```perl
> my ($x, $y, $z) = 2, 3, 4
> say $x # 2
> say $y # 3
> say $z # 4
> ```
> Here, the parentheses in `($x, $y, $z)` cannot be ignored.

### List Assignment

```perl
my ($x, $y, $z);
($x, $y, $z) = f();  # if f() returns (2, 3, 7) then it is nearly the same as $x=2; $y=3; $z=7;
($x, $y, $z) = f();  # if f() returns (2, 3, 7, 9), then ignore 9
($x, $y, $z) = f();  # if f() returns (2, 3);  then $z will be undef

```

How can we swap the values of 2 variables, let say $x and $y? 

> Note：  
> ```perl
> my $x = 233
> my $y = 666
> ($x, $y) = ($y, $x)
> say $x # 666
> say $y # 233
> ```

### Swap two values

**tutorial/arrays/lists.p6**

```perl
#!/usr/bin/env perl6
use v6;

say "Type in two values:";
my $a = $*IN.get;
my $b = $*IN.get;

($a, $b) = ($b, $a);
say $a;
say $b;
```

### Loop over elements of list with for

**tutorial/arrays/arrays.p6**

```perl
#!/usr/bin/env perl6
use v6;

for "Foo", "Bar", "Baz" -> $name {
    say $name;
}

say "---";

for 1..5 -> $i {
    say $i;
}

say "---";

for 1..Inf -> $i {
    say $i;
    last if $i > 3;
}

say "---";

for 1..* -> $i {
    say $i;
    last if $i > 3;
}
```

### Create array, go over elements

Arrays start with am at mark @ and the name of the array. You can assign a list of values to the array. 

As you can see the parentheses () around the list values are optional.

**tutorial/arrays/list_colors_array.p6**  

```perl
#!/usr/bin/env perl6
use v6;

my @colors = "Blue", "Yellow", "Brown", "White";
say @colors;

say "--------------------------------";

say "@colors";                                       # says "@colors" directly!

say "--------------------------------";

say "{@colors}";                                     # says the items in the list with spaces as separators

say "--------------------------------";

say "@colors[]";                                     # says the items in the list with spaces as separators

say "--------------------------------";

for @colors -> $color {
    say $color;
}
```

### Array elements (create menu)

**tutorial/arrays/color_menu.p6**  
```perl
#!/usr/bin/env perl6
use v6;

my @colors = <Blue Yellow Brown White>;

for 1..@colors.elems -> $i {
    say "$i) @colors[$i-1]";
}

my $input = prompt("Please select a number: ");
say "You selected @colors[$input-1]";

```

### Array assignment

**tutorial/arrays/array_assignment.p6**  
```perl
#!/usr/bin/env perl6
use v6;

my $owner = "Moose";
my @tenants = "Foo", "Bar";
my @people = ($owner, 'Baz', @tenants);  # the array is flattened:
say "{@people}";                         # Moose Baz Foo Bar
@people.say                              # [Moose Baz [Foo Bar]]

my ($x, @y)     = (1, 2, 3, 4);
say $x;                              # $x = 1
say "{@y}";                          # @y = (2, 3, 4)
```

### Command line options

- `@*ARGS` is the array maintained by language that holds the values supplied on the comman line. 
- It does NOT include the name of the program. 
- Only the argument after the program. 
- The name of the program, and the relative path to it, is in the variable `$*PROGRAM-NAME`. 

**tutorial/arrays/color_menu_option.p6**

```perl
#!/usr/bin/env perl6
use v6;

my $color = @*ARGS[0];

if not $color {
    my @colors = <Blue Yellow Brown White>;

    for 1 .. @colors.elems -> $i {
        say "$i) @colors[$i-1]";
    }

    my $input = prompt "Please select a number: ";
    $color = @colors[$input-1];
}

say "You selected $color";
```

### Process CSV file

**tutorial/arrays/sample_csv_file.csv**

```
Foo,Bar,10,home
Orgo,Morgo,7,away
Big,Shrek,100,US
Small,Fiona,9,tower
```

**tutorial/arrays/process_csv_file.p6**

```perl
#!/usr/bin/env perl6
use v6;

my $file = 'examples/arrays/sample_csv_file.csv';
if defined @*ARGS[0] {
    $file = @*ARGS[0];
}

my $sum = 0;
my $data = open $file;
for $data.lines -> $line {
    my @columns = split ",", $line;
    $sum += @columns[2];
}
say $sum;
```

### join

**tutorial/arrays/join.p6**

```perl
#!/usr/bin/env perl6
use v6;

my @fields = <Foo Bar foo@bar.com>;
my $line = join ";", @fields;
say $line;     # Foo;Bar;foo@bar.com
```

### The unique function

**tutorial/arrays/unique.p6**

```perl
#!/usr/bin/env perl6
use v6;

my @duplicates = 1, 1, 2, 5, 1, 4, 3, 2, 1;
say @duplicates.perl;
#[1, 1, 2, 5, 1, 4, 3, 2, 1]

my @unique = unique @duplicates;
say @unique.perl;
#[1, 2, 5, 4, 3]

my @chars = <b c a d b a a a b>;
say @chars.perl;
#["b", "c", "a", "d", "b", "a", "a", "a", "b"]

my @singles = unique @chars;
say @singles.perl;
#["b", "c", "a", "d"]
```

### Looping over a list of values

In Perl 6 the standard way to iterate over the elements of a list or an array is by using the `for` statement. 

**tutorial/arrays/loop_over_array.p6**

```perl
#!/usr/bin/env perl6
use v6;

my @fellows = <Foo Bar Baz>;
for @fellows -> $name {
    say $name;
}
```

The loop variable (`$name`) in the above case is automatically declared in the loop so one does not need to declare it using `my` and it is still not global.


### Looping over any number of elements

You can also iterate over any number of elements:

**tutorial/arrays/looping_over_many_elements.p6**

```perl
#!/usr/bin/env perl6
use v6;

my @scores = <
    Valencia  1 1 Recreativo_Huelva
    Athletic_Bilbao 2 5 Real_Madrid
    Malaga      2  2      Sevilla_FC
    Sporting_Gijon  3 2 Deportivo_La_Coruna
    Valladolid     1  0     Getafe
    Real_Betis     0  0     Osasuna
    Racing_Santander     5  0     Numancia
    Espanyol     3  3     Mallorca
    Atletico_Madrid     3  2     Villarreal
    Almeria     0  2     Barcelona
>;

for @scores -> $home, $home_score, $guest_score, $guest {
    say "$home $guest $home_score : $guest_score";
}
```

> Note：  
> ```
> Valencia Recreativo_Huelva 1 : 1
> Athletic_Bilbao Real_Madrid 2 : 5
> Malaga Sevilla_FC 2 : 2
> Sporting_Gijon Deportivo_La_Coruna 3 : 2
> Valladolid Getafe 1 : 0
> Real_Betis Osasuna 0 : 0
> Racing_Santander Numancia 5 : 0
> Espanyol Mallorca 3 : 3
> Atletico_Madrid Villarreal 3 : 2
> Almeria Barcelona 0 : 2
> ```

### Missing values

One should ask the question what happens if the list runs out of values in the middle, of a multi-value iteration?

**tutorial/arrays/missing_values.p6**

```perl
#!/usr/bin/env perl6
use v6;

for (1, 2, 3, 4, 5) -> $x, $y {
    say "$x $y";
}
say 'done';
```

In this case Rakudo throws an exception when it finds out it does not have enough values for the last iteration.

**tutorial/arrays/missing_values.p6.output**

```
1 2
3 4
Not enough positional parameters passed; got 1 but expected 2
  in block <anon> at examples/arrays/missing_values.p6:4
```

In order to avoid the exception we could tell the loop that the second and subsequent values are optional.


**tutorial/arrays/missing_values_fixed.p6**

```perl
#!/usr/bin/env perl6
use v6;

for (1, 2, 3, 4, 5) -> $x, $y? {
    say "$x $y";
}
say 'done';
```

output：

```
1 2
3 4
Use of uninitialized value of type Mu in string context.
Methods .^name, .perl, .gist, or .say can be used to stringify it to something meaningful.
   in block <anon> at examples/arrays/missing_values_fixed.p6:5
5
done
```

> [How to be aware of missing values during multi-value iteration in Perl 6?](https://stackoverflow.com/questions/44224878/how-to-be-aware-of-missing-values-during-multi-value-iteration-in-perl-6/44225499#44225499)。
>

### Iterating over more than one array in parallel

What if you have two (or more) array you'd like to combine somehow? 

**tutorial/arrays/z.p6**

```perl
#!/usr/bin/env perl6
use v6;

my @chars   = <a b c>;
my @numbers = <1 2 3>;

for @chars Z @numbers -> ($letter, $number) {
    say "$letter $number";
}
```

output:

**tutorial/arrays/z.p6.out**

```
a 1
b 2
c 3
```

### Z as in zip

The Z infix operator version of the `zip` function allows the parallel use of two lists:

**tutorial/arrays/z_on_more.p6**

```perl
#!/usr/bin/env perl6
use v6;

my @operator  = <+ - *>;
my @left      = <1 2 3>;
my @right     = <7 8 9>;

for @left Z @operator Z @right -> ($a, $o, $b) {
    say "$a $o $b";
}
```

output:

**tutorial/arrays/z_on_more.p6.out**

```perl
1 + 7
2 - 8
3 * 9
```

### xx - string multiplicator

**tutorial/arrays/xx.p6**

```perl
#!/usr/bin/env perl6
use v6;

my @moose = "moose" xx 3;
say "{@moose}"; # moose moose moose
```

### sort values

**tutorial/arrays/sort.p6**

```perl
#!/usr/bin/env perl6
use v6;

my @names = <foo bar moose bu>;
my @sorted_names = sort @names;
say @sorted_names.perl;   # ["bar", "bu", "foo", "moose"]


my @numbers = 23, 17, 4;
my @sorted_numbers = sort @numbers;
say @sorted_numbers.perl;   # [4, 17, 23]


my @sort_names_by_length = sort { $^a.chars <=> $^b.chars }, @names;
say @sort_names_by_length.perl;   # ["bu", "foo", "bar", "moose"]

#the same result with one sub (Schwartzian transformation)
my @sort_1 = sort { $_.chars }, @names;
say @sort_1.perl;     # ["bu", "foo", "bar", "moose"]

my @sort_2 = @names.sort({ $_.chars });
say @sort_2.perl;     # ["bu", "foo", "bar", "moose"]

my @sort_3 = @names.sort: { $_.chars };
say @sort_3.perl;     # ["bu", "foo", "bar", "moose"]


my @words = <moo foo bar moose bu>;
say @words.sort({ $^a.chars <=> $^b.chars}).perl; # ["bu", "moo", "foo", "bar", "moose"];

say @words.sort({ $^a.chars <=> $^b.chars or $^a cmp $^b}).perl; # ["bu", "bar", "foo", "moo", "moose"];

#TODO: should be also:
#say @words.sort({ $^a.chars <=> $^b.chars }, {$^a cmp $^b}).perl; # ["bu", "bar", "foo", "moo", "moose"];
#say @words.sort({ $_.chars },  {$^a cmp $^b}).perl; # ["bu", "bar", "foo", "moo", "moose"];
```


### Transform arrays and lists using map

#### map as a function

**tutorial/arrays/map_double_function.p6**

```perl
use v6;

my @numbers = 2, 5, 7;

my @d = map { $_ * 2 }, @numbers;
say @d.perl;          # [4, 10, 14]

my @x = map { $^number * 2 }, @numbers;
say @x.perl;          # [4, 10, 14]

say @numbers.perl; # [2, 5, 7]
```

- The variable `$_` contains the current element of the array. 
- The map assigns each element to `$_` variable, executes the code in the block.
- The collected list is assigned to the array on the left hand side of the `=` assignment.

- In the second example, we used another variable type that is auto-generated. 
- We could have used any variable name there. 
- The caret `^` after the `$-sign` told perl that this is an auto-generated variable. 
- This has exactly the same job as `$_` but if you select a good name will make your code much more readable.
- The perl method helps us to dump the content of the array. 

#### map as a method

The same functionality is available as a method as well. 

**tutorial/arrays/map_double_method.p6**

```perl
use v6;

my @numbers = 2, 5, 7;

my @d = @numbers.map( -> $x {$x * 2 } );
say @d.perl;      # [4, 10, 14]

my @b = @numbers.map( * * 2 );
say @b.perl;     # [4, 10, 14]

say @numbers.perl; # [2, 5, 7]
```

- In the first call we used the **arrow operator** to create an internal variable. 
- Much like it is done in regular **for-loops**. 
- Then, just as in a for-loop a block follows with the actual expression.
- In the second expression the first `*` is the **Whatever operator**, which is anothe placeholder of the `current value`. 
- I think this version is not very readable
