## Files in Perl 6
 
### exit()

**tutorial/files/exit.p6**
```perl
#!/usr/bin/env perl6
use v6;

say "hello";

exit;

say "world";
```

### warn()

`warn()` will print a message on standard error (STDERR) and go on running. 

**tutorial/files/warn.p6**
```perl
#!/usr/bin/env perl6
use v6;

warn "This is a warning";

say "Hello World";
```

### die()

`die()`will print the message on the standard error and stop the execution of the program. 

**tutorial/files/die.p6**
```perl
#!/usr/bin/env perl6
use v6;

say "Before calling die";

die "This will kill the script";

say "This will not show up";
```

### Twigils and special variables

> Perl 6 has a large set of special variables. 
> In order to make it easy to differentiate them from regular variables, they are marked with a second prefix called twigil.
> Regular, user defined variables have a sigil ($, @ or %) in front of them. 

Examples:  

- `$*PROGRAM-NAME`: relative path to the currently running Perl 6 script.
- `$*PROGRAM`: full path to the currently running Perl 6 cript.
- `$*CWD`: contains a path to the current working directory.
- `$*IN`: is the standard input (STDIN). You can read a line using $*IN.get.
- `$*PID`: Process ID.
- `$*EXECUTABLE-NAME`: Relative path to the current binary. Most likely perl6.
- `$*EXECUTABLE`: Full path to perl6.
- `$*TMPDIR`: full path to the temp file directory.


### Read line from file

In Perl 6 it is done by the `open()` function imported from the `IO` class.

Two parameters are important: The name of the file and the mode. In order to open a file for reading the mode need to be :r.  

`$fh = open $filename, :r`  

Once we have an open file handler we can use the get method (`$fh.get`) to read one line from the given file.

One could read many lines using consecutive calls to the get method but there are better ways to do that. 

**tutorial/files/read_one_line.p6**
```perl
#!/usr/bin/env perl6
use v6;

my $filename = $*PROGRAM-NAME;

my $fh = open $filename;
my $line = $fh.get;
say $line;
```

### Read all the lines with get ()

**tutorial/files/read_file_line_by_line.p6**
```perl
#!/usr/bin/env perl6
use v6;

my $filename = $*PROGRAM-NAME;

my $fh = open $filename;
while (defined my $line = $fh.get) {
    say $line;
}
```

### Process a file line by line

**tutorial/files/read_file.p6**
```perl
#!/usr/bin/env perl6
use v6;

my $filename = $*PROGRAM-NAME;

my $fh = open $filename;
for $fh.lines -> $line {
    say $line;
}
```

- The `lines()` method of the file handle can return either all the lines, some of the lines. 

- As it does it lazily in the above code we get an iterator behavior so the file is read line-by-line into the `$line` variable by the for loop.

- This script is very similar to what the unix `cat` command does. 


### Write to a file

- In order to write to a file first we need to open the file for writing with the `:w` mode. 

- If this is successful we get back a `file handler` on which we can use the regular output methods such as `print()`, `say()` or `printf()`. 

**tutorial/files/write_file.p6**
```perl
#!/usr/bin/env perl6
use v6;

my $filename = "temp.txt";

my $fh = open $filename, :w;
$fh.say("hello world");
$fh.close;
```

### Open file modes

<table>
<tr>
    <td>:r</td>
    <td>read-only mode</td>
</tr>
<tr>
    <td>:w</td>
    <td>write-only mode</td>
</tr>
<tr>
    <td>:rw</td>
    <td>read-write mode</td>
</tr>
<tr>
    <td>:a</td>
    <td>append mode</td>
</tr>
</table>  


### slurp() and spurt()

Perl 6 has a built in mode to slurp in files, that is to read the contents of a whole file into a scalar variable.  

**tutorial/files/slurp_file.p6**
```perl
#!/usr/bin/env perl6
use v6;

my $filename = $*PROGRAM-NAME;

my $data = slurp $filename;
say $data;
```

> Noteï¼š  
> Perl6 has a built in mode to spurt data to file.  
> ```perl
> my $newdata = "New scores:
> Paul 10
> Paulie 9
> Paulo 11";
> 
> spurt "newdatafile.txt", $newdata;
> ```  

### Read lines into array

When we put the result of the `slurp()` call into an array. all the content of the file ends up in as the first and only element of the array. 

If you want to read every line into a separate element of the array you need to use the `lines()` function.

**tutorial/files/read_file_into_array.p6**
```perl
#!/usr/bin/env perl6
use v6;

my $filename = $*PROGRAM-NAME;

#reads all the content of the file in the first element of the array!
my @content = slurp $filename;
say @content.elems;

#reads all the content of the file, every line an element in the array
my @rows = lines $filename.IO;
say @rows.elems;
```

### Exercise: Print sum of numbers

Take a file that has one number on every line. Print the sum of the numbers.

**tutorial/numbers.txt**
```
3
8
19
-7
13
```  

Print average, minimum and maximum  

**tutorial/files/statistics.p6**
```perl
#!/usr/bin/env perl6
use v6;

my $filename = 'examples/numbers.txt';

my $total;
my $count;
my $min;
my $max;

if (my $fh = open $filename, :r) {
    for $fh.lines -> $line {
        if (not $count) {
            $min = $max = $line;
        }
        $total += $line;
        if ($min > $line) {
            $min = $line;
        }
        if ($max < $line) {
            $max = $line;
        }
        $count++;
    }
    my $average = $total / $count;
    say "Total: $total, Count: $count Average: $average Min: $min Max: $max";
} else {
    say "Could not open '$filename'";
}

#There is a minor issue in this solution, what if there are no values at all in the file?
```
