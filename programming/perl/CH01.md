## CH01 First steps in Perl 6

### Hello World - scalar variables

Strings can be stored in so called scalar variables. Each such variable starts with a `$` sign and then alphanumeric characters, underscore or dash. Before using such a variable the first time one has to declare it by the `my` keyword.   
  
> *A scalar is a single thing. —— Learning Perl, 7th Edition*

**tutorial/scalars/hello_world_variable.p6**  

```perl
#!/usr/bin/env perl6
use v6;

my $greeting = "Hello World";
say $greeting;

my $Gábor-was-born-in = 'Hungary';
say $Gábor-was-born-in;
```

By default scalar variables have no specific types but later we will see how can we restrict the values a scalar can hold to a number or to any other type.

One can also declare a variable without giving it an inital value, : 

```perl
my $x;
```

n such case the value of the variable will be `(Any)` which is just case of being **not defined**. 

> `my @array[3]; # [(Any) (Any) (Any)]`  
> `my $var; # (Any)`  
> *Any is the class that serves as a default base class for new classes, and as the base class for most built-in classes. —— [class Any](https://docs.perl6.org/type/Any)*。


### Hello World - interpolation

We can put scalar variables within regular strings. Their value will be interpolated into the string. 

**tutorial/scalars/hello_world_variable_interpolation.p6**
```perl
#!/usr/bin/env perl6
use v6;

my $name     = "Foo";
my $greeting = "Hello $name";
say $greeting;
```

print

```
Hello Foo
```

### Reading from the keyboard

Perl 5 users could think about it as having `autochomp` 

**tutorial/scalars/read_stdin.p6**
```perl
#!/usr/bin/env perl6
use v6;

my $name = prompt("Please type in yourname: ");

say "Hello $name";
```

### Numerical operations

Numerical operator can be used on the scalar values. 

**tutorial/scalars/numerical_operators.p6**  

```perl
#!/usr/bin/env perl6
use v6;

my $x = 3;
my $y = 11;

my $z = $x + $y;
say $z;            # 14

$z = $x * $y;
say $z;            # 33
say $y / $x;       # 3.66666666666667

$z = $y % $x;      # (modulus)
say $z;            # 2

$z += 14;          # is the same as   $z = $z + 14;
say $z;            # 16

$z++;              # is the same as   $z = $z + 1;
$z--;              # is the same as   $z = $z - 1;

$z = 23 ** 2;      # exponentiation
say $z;            # 529
```

### Automatically convert string to number

Perl 6 does not care that you typed in a string, the numeric + adds together the two numbers. 

**tutorial/scalars/add.p6**  

```perl
#!/usr/bin/env perl6
use v6;

my $a = prompt "First number:";
my $b = prompt "Second number:";

my $c = $a + $b;

say "\nResult: $c";
```

### String operators

**tutorial/scalars/string_operators.p6**  
```perl
#!/usr/bin/env perl6
use v6;

my $x = "Hello";
my $y = "World";

my $z = $x ~ " " ~ $y;  # the same as "$x $y"
say $z;                 # Hello World


my $w = "Take " ~ (2 + 3);
say $w;                        # Take 5
say "Take 2 + 3";              # Take 2 + 3
say "Take {2 + 3}";            # Take 5



$z ~= "! ";             #  the same as  $z = $z ~ "! ";
say "'$z'";             # 'Hello World! '
```

`~` concatenates two strings.

Any operation can be interpolated into a string using the curly brace syntax.


### String concatenation

**tutorial/scalars/concat.p6**  
```perl
#!/usr/bin/env perl6
use v6;

my $a = prompt "First string:";
my $b = prompt "Second string:";

my $c = $a ~ $b;

say "\nResult: $c";
```

### String repetition

**tutorial/scalars/string_repetition.p6**  

```perl
#!/usr/bin/env perl6
use v6;

my $z = "Hello World! ";

my $q = $z x 3;
say "'$q'";         # 'Hello World! Hello World! Hello World! '
```

### if statement - comparing values

**tutorial/scalars/if.p6**  

```perl
#!/usr/bin/env perl6
use v6;

my $age = 23;
if $age > 18 {
    say "You can vote in most countries.";
}
```

### Ternary Operator

**tutorial/scalars/ternary.p6**  

```perl
use v6;

my $age = 42;

if $age > 18 {
    say "Above 18";
} else {
    say "Below 18";
}

say $age > 18 ?? "Above 18" !! "Below 18";
```

```
COND ?? VALUE_IF_TRUE !! VALUE_IF_FALSE
```

### Comparison Operators

Two sets of relation operators. 

- One is to compare numerically 
- the other is to compare as strings, based on the ASCII table. 

```perl
3 == 4               # false
'35' eq 35.0         # false
'35' == 35.0         # true
13 > 2               # true
13 gt 2              # false !!!
"hello" == "world"   # throws exception
"hello" eq "world"   # false
"hello" == ""        # throws exception
"hello" eq ""        # false
```

### Boolean expressions (logical operators)

<table>
<tr>
    <td>symbol</td>
    <td>key word</td>
</tr>
<tr>
    <td>&&</td>
    <td>and</td>
</tr>
<tr>
    <td>||</td>
    <td>or</td>
</tr>
<tr>
    <td>//</td>
    <td>orelse</td>
</tr>
<tr>
    <td>^^</td>
    <td>xor</td>
</tr>
<tr>
    <td>!</td>
    <td>not</td>
</tr>
</table>  

> Note：  
> - or No matter the left COND value, it will still check the right COND value.  
> - orelse When the left COND true, it doesn't check the right COND value.
```
if COND and COND {
}

if COND or COND {
}

if not COND {
}
```

**tutorial/scalars/logical_operators.p6** 

```perl
#!/usr/bin/env perl6
use v6;

say (2 and 1);  # 1
say (1 and 2);  # 2
say (1 and 0);  # 0
say (0 and 1);  # 0
say (0 and 0);  # 0
say "---";

say (1 or 0);   # 1
say (1 or 2);   # 1
say (0 or 1);   # 1
say (0 or 0);   # 0
say "---";

say (1 // 0);     # 1
say (0 // 1);     # 0
say (0 // 0);     # 0
say "---";

say (1 xor 0);     # 1
say (0 xor 1);     # 1
say (0 xor 0);     # 0
say (1 xor 1);     # Nil
say "---";

say (not 1);       # False
say (not 0);       # True
say "---";
```

### Chained comparisons

**tutorial/scalars/chained_comparison.p6**
```perl
#!/usr/bin/env perl6
use v6;

my $a = prompt "Type in a number between 23 and 42: ";
if 23 <= $a and $a <= 42 {
    say "Good, $a is in the range.";
} else {
    say "Did I say between 23 and 42 ?";
}

my $small = prompt "Type another number between 0 and $a: ";
my $big = prompt "Type another number between $a and 100: ";

if 0 <= $small <= $a <= $big <= 100 {
    say "good";
} else {
    say "something is fishy";
}
```

### 一个简易计算器 - 使用值的比较
**tutorial/scalars/calculator.p6**  

```perl
#!/usr/bin/env perl6
use v6;

my $a        = prompt "Number:";
my $operator = prompt "Operator: [+-*/]:";
my $b        = prompt "Number:";

if $operator eq "+" {
    say $a + $b;
} elsif $operator eq "-" {
    say $a - $b;
} elsif $operator eq "*" {
    say $a * $b;
} elsif $operator eq "/" {
    say $a / $b;
} else {
    say "Invalid operator $operator";
}
```

### Comparing values - Calculator

The given - when construct (known in other languages as case or switch)

**tutorial/scalars/calculator_given.p6**
```perl
#!/usr/bin/env perl6
use v6;

my $a        = prompt "Number:";
my $operator = prompt "Operator: [+-*/]:";
my $b        = prompt "Number:";

if $operator eq "+" {
    say $a + $b;
} elsif $operator eq "-" {
    say $a - $b;
} elsif $operator eq "*" {
    say $a * $b;
} elsif $operator eq "/" {
    say $a / $b;
} else {
    say "Invalid operator $operator";
}

given $operator {
    when "+" { say $a + $b; }
    when "-" { say $a - $b; }
    when "*" { say $a * $b; }
    when "/" { say $a / $b; }
    default  { say "Invalid operator $operator"; }
}
```

### String functions: index

**tutorial/scalars/string_functions_index.p6**
```perl
#!/usr/bin/env perl6
use v6;

my $s = "The black cat jumped from the green tree";

say index $s, "e";                          # 2
say index $s, "e", 3;                       # 18

say rindex $s, "e";                         # 39
say rindex $s, "e", 38;                     # 38
say rindex $s, "e", 37;                     # 33
```

### String functions: substr

**tutorial/scalars/string_functions_substr.p6**
```perl
#!/usr/bin/env perl6
use v6;

my $s = "The black cat climbed the green tree";
my $z;
$z = substr $s, 4, 5;                    # $z = black
say $z;
$z = substr $s, 4, *-11;                 # $z = black cat climbed the
say $z;
$z = substr $s, 14;                      # $z = climbed the green tree
say $z;
$z = substr $s, *-4;                     # $z = tree
say $z;
$z = substr $s, *-4, 2;                  # $z = tr
say $z;
```

### Super-or (junctions)

**tutorial/scalars/junctions.p6**
```perl
#!/usr/bin/env perl6
use v6;

say "Please select an option:";
say "1) one";
say "2) two";
say "3) three";
my $c = prompt('');

if $c == 1 or $c == 2 or $c == 3 {
    say "correct choice: $c";
} else {
    say "Incorrect choice: $c";
}

if $c == 1|2|3 {
    say "correct choice: $c";
} else {
    say "Incorrect choice: $c";
}
```
